///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2025, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьНастроек();
	
	// Получим пользователей ИБ
	СписокПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
	СписокВыбораПользователей = Элементы.гпр_ПользовательБазы.СписокВыбора;
	
	Для Каждого ТекущийПользователь Из СписокПользователей Цикл
		СписокВыбораПользователей.Добавить(ТекущийПользователь.Имя);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(НаборКонстант.гпр_РежимВыгрузкиКонфигурации) Тогда
		Отказ = Истина;
		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = НСтр("ru ='Не указан режим выгрузки конфигурации.'");
		СообщениеПользователю.Поле = "гпр_РежимВыгрузкиКонфигурации";
		СообщениеПользователю.УстановитьДанные(ЭтотОбъект);
		СообщениеПользователю.Сообщить();
		Возврат;
	ИначеЕсли НаборКонстант.гпр_РежимВыгрузкиКонфигурации = 
		ПредопределенноеЗначение("Перечисление.гпр_РежимыВыгрузкиКонфигурации.КонфигурацияВыгруженаВКаталог")
		И ПустаяСтрока(НаборКонстант.гпр_КаталогВыгрузкиКонфигурации) Тогда
		Отказ = Истина;
		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = НСтр("ru ='Не указан каталог выгрузки конфигурации в файлы.'");
		СообщениеПользователю.Поле = "гпр_КаталогВыгрузкиКонфигурации";
		СообщениеПользователю.УстановитьДанные(ЭтотОбъект);
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗакрытьБезЗаписиНастроек 
		И (Не ЗначениеЗаполнено(НаборКонстант.гпр_РежимВыгрузкиКонфигурации)
			ИЛИ Модифицированность) Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		ОбработчикОповещения = Новый ОписаниеОповещения("ЗавершениеВопросаОЗакрытииНастроек", ЭтотОбъект);
		ПоказатьВопрос(
			ОбработчикОповещения,
			НСтр("ru = 'Закрытие формы приведет к отмене заполнения прав доступа по роли. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
		Возврат;
	ИначеЕсли ЗакрытьБезЗаписиНастроек Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура гпр_РежимВыгрузкиКонфигурацииПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УстановитьВидимостьНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура гпр_КаталогВыгрузкиКонфигурацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Каталог = НаборКонстант.гпр_КаталогВыгрузкиКонфигурации;
	
	Обработчик = Новый ОписаниеОповещения(
		"ВыборКаталогаXMLПродолжение",
		ЭтотОбъект);
	
	ДиалогВыбораФайла.Показать(Обработчик);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьНастроек()
	
	Элементы.гпр_КаталогВыгрузкиКонфигурации.Видимость = (НаборКонстант.гпр_РежимВыгрузкиКонфигурации = 
		Перечисления.гпр_РежимыВыгрузкиКонфигурации.КонфигурацияВыгруженаВКаталог);
	Элементы.ПользовательПароль.Видимость = (НаборКонстант.гпр_РежимВыгрузкиКонфигурации =
		Перечисления.гпр_РежимыВыгрузкиКонфигурации.КонфигурацияНеВыгруженаВКаталог);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаXMLПродолжение(РезультатВыбора, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВыбора <> Неопределено 
		И РезультатВыбора.Количество() > 0 Тогда
		НаборКонстант.гпр_КаталогВыгрузкиКонфигурации = РезультатВыбора[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВопросаОЗакрытииНастроек(Ответ, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗакрытьБезЗаписиНастроек = Истина;
		Результат = Новый Структура();
		Результат.Вставить("ПрерватьЗаполнение", Истина);
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
