///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2025, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// См. гпр_ИспортЭкспортРолей.СохранитьРоли
Процедура СохранитьРолиНаДиск(Знач Роли) Экспорт
	
	Результат = гпр_ИспортЭкспортРолейВызовСервера.СохранитьРоли(Роли);
	Если Роли.Количество() = 1 Тогда
		ИмяФайла = гпр_ИспортЭкспортРолейВызовСервера.ИмяРоли(Роли[0]);
	Иначе
		ИмяФайла = "Роли";
	КонецЕсли;
	
	НачатьПолучениеФайлаССервера(Результат, СтрШаблон("%1.json", ИмяФайла));
	
КонецПроцедуры

// Разбор файла с ролями в формате json и запись данных в базу.
// 
// Параметры:
//  УникальныйИдентификаторФормы - УникальныйИдентификатор - Уникальный идентификатор формы для сохранения во временном
//                                                           хранилище
Процедура ЗагрузитьРоли(УникальныйИдентификаторФормы) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьРолиЗавершениеЗагрузкиФайла", гпр_ИспортЭкспортРолейКлиент);
	НачатьПомещениеФайлаНаСервер(ОписаниеОповещения,,,,, УникальныйИдентификаторФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗагрузитьРолиЗавершениеЗагрузкиФайла(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеПомещенногоФайла = Неопределено Или ОписаниеПомещенногоФайла.ПомещениеФайлаОтменено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	гпр_ИспортЭкспортРолейВызовСервера.ЗагрузитьРоли(ОписаниеПомещенногоФайла.Адрес);
	ПоказатьПредупреждение(, "Загрузка ролей завершена");
	
КонецПроцедуры

#КонецОбласти