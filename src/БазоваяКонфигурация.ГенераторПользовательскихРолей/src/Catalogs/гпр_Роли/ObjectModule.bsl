

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЭтоГруппа И Не ЗначениеЗаполнено(ИдентификаторРоли) Тогда
		
		ИдентификаторРоли = Новый УникальныйИдентификатор();
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ИмяРолиВКонфигурации = СокрЛП(ИмяРолиВКонфигурации);
	
	ПроверитьИмяРоли(Отказ);
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если гпр_РаботаСКонфигурациейСервер.НайденаРольПоИмени(Ссылка, ИмяРолиВКонфигурации) Тогда
		
		ВызватьИсключение НСтр("ru = 'Роль с таким именем в конфигурации уже существует.'");
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьИмяРоли(Отказ)
	
	Буквы = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯABCDEFGHIJKLMNOPQRSTUVWXYZ";
	Цифры = "1234567890";
	
	Если СтрНайти(Буквы + "_", ВРег(Лев(ИмяРолиВКонфигурации, 1))) = 0
		ИЛИ СтрРазделить(ВРег(ИмяРолиВКонфигурации), Буквы + Цифры + "_", Ложь).Количество() <> 0 Тогда
		
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "ИмяРолиВКонфигурации";
		Сообщение.Текст = НСтр("ru = 'Не верное имя! Имя должно состоять из одного слова, начинаться с буквы и не содержать символов кроме ""_"".'");
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
